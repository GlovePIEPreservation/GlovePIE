// Carl's version of Bob's Wii Drums! No other program required.
// www.bobsomers.com

var.xOffset = 8
var.yOffset = -37
var.zOffset = 12

var.xRot = Wiimote.RawForceX + var.xOffset
var.yRot = Wiimote.RawForceY + var.yOffset
var.zRot = Wiimote.RawForceZ + var.zOffset

// Debug
debug = "X=" + var.xRot + "   Y=" + var.yRot + "   Z=" + var.zRot

// Downward drum beat triggered
if var.yRot > 30 then
   var.B = Wiimote.B and not (Wiimote.A or Wiimote.Minus or Wiimote.Home or Wiimote.Plus)
   var.S = Wiimote.A and not (Wiimote.B or Wiimote.Minus or Wiimote.Home or Wiimote.Plus)
   var.C = Wiimote.A and Wiimote.B and not (Wiimote.Minus or Wiimote.Home or Wiimote.Plus)
   var.T = Wiimote.Minus and not (Wiimote.A or Wiimote.B or Wiimote.Home or Wiimote.Plus)
   var.M = Wiimote.Home and not (Wiimote.A or Wiimote.B or Wiimote.Minus or Wiimote.Plus)
   var.L = Wiimote.Plus and not (Wiimote.A or Wiimote.B or Wiimote.Minus or Wiimote.Home)
   var.H = not(Wiimote.A or Wiimote.B or Wiimote.Minus or Wiimote.Home or Wiimote.Plus)
else
    var.B = false
    var.S = false
    var.H = false
    var.C = false
    var.T = false
    var.M = false
    var.L = false
endif

Midi.volume = 100%
Midi.BassDrum1 = var.B
Midi.AcousticSnare = var.S
Midi.CrashCymbal1 = var.C
Midi.HighMidTom = var.T
Midi.LowMidTom = var.M
Midi.LowTom = var.L
Midi.ClosedHiHat = var.H

// note, unfortunately you can't set those Midi values inside the IF
// because IF statements in GlovePIE cause it to send the midi message
// again even if it hasn't changed, whereas outside IF statements it
// only sends a MIDI message if it has changed.
