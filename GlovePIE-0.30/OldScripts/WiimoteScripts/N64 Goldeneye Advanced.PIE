//Configuration for Goldeneyy
//Written by vkapadia
//vkapadia@vkapadia.com
//
//PPJoy is required.
//Make a Virtual Joystick, set at least 2 axes, and at least 9 buttons.
//Make the axes Analog0 and Analog1, and the buttons Digital1 through Digital9.
//
//Set your emulator to these values:
//Analog Stick = Joystick Left/Right/Up/Down
//Start = S
//Z-Button = Z
//R-Button = R
//A-Button = A
//B-Button = B
//C-Up = Numpad 8
//C-Down = Numpad 2
//C-Left = Numpad 4
//C-Right = Numpad 6
//
//Controls are as follows:
//
//Press 1 to use Menu Mode, 2 to use Game Mode
//LED 1 lights up for Menu Mode, LED 4 lights up for Game Mode
//
//For Menus:
//Moving the Wiimote Up and Down = Analog Stick Up and Down
//Rotating the Wiimote Left and Right = Analog Stick Left and Right
//B-Button = Select Item
//
//In Game:
//Moving the Wiimote Up and Down = Analog Stick Up and Down
//Rotating the Wiimote Left and Right = C Left and C Right
//D-Pad Up and Down = C Up and C Down
//D-Pad Left and Right = Analog Stick Left and Right
//A-Button = R-Button (when in this mode, use the Wiimote to look around)
//B-Button = Z-Button
//Minus = A-Button
//Plus = B-Button
//Home = Start

if wiimote.One
var.mode = 1
endif
if wiimote.Two
var.mode = 2
endif

S = wiimote.Home
Z = wiimote.B
A = wiimote.Minus
B = wiimote.Plus
R = wiimote.A

NUMPAD4 = wiimote.Left
NUMPAD6 = wiimote.Right
PPJoy.Analog0 = -(wiimote.RawForceX + 6) / 20

if wiimote.A or var.mode = 1 then
PPJoy.Analog1 = (wiimote.RawForceZ - 3) / 15
NUMPAD8 = wiimote.Up
NUMPAD2 = wiimote.Down
wiimote.Leds = 1
else
if wiimote.Up then
PPJoy.Analog1 = 1
elseif wiimote.Down then
PPJoy.Analog1 = -1
else
PPJoy.Analog1 = 0
endif
NUMPAD8 = wiimote.RawForceZ < 0
NUMPAD2 = wiimote.RawForceZ > 10
wiimote.Leds = 8
endif

