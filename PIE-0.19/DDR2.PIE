// Uncomment this to enable Pump It Up
// var.Pump = true

// Disable all filtering (we NEED accurate timing)
p5.AveragingFilter = true
p5.DeadbandFilter = false
p5.FilterDistance = 0
p5.FilterFrames = 2

var.RightFootX = (glove1.x in cm) + 4.5cm + 1cm
var.RightFootY = (glove1.y in cm)  + 9cm
var.RightFootZ = (glove1.z in cm)   + 60cm

if glove1.FilterPitch<-40 then var.RightFootY = var.RightFootY - 1
if glove1.FilterPitch<-70 then var.RightFootY = var.RightFootY - 1
if var.RightFootZ < -22 then var.RightFootY = var.RightFootY -1
if var.RightFootX < -22 then var.RightFootY = var.RightFootY -2
if var.RightFootX > 22 then var.RightFootY = var.RightFootY -1

var.RightFootFloor = ((Var.RightFootY < 6) and (var.RightWasOnFloor)) or (Var.RightFootY < 4)
var.RightWasOnFloor = var.RightFootFloor

var.LeftFootX = (glove2.x in cm) - 4.5cm - 1cm
var.LeftFootY = (glove2.y in cm) + 9cm
var.LeftFootZ = (glove2.z in cm)  + 60cm

if glove2.FilterPitch<-60 then var.RightFootY = var.RightFootY - 1
if glove2.FilterPitch<-80 then var.RightFootY = var.RightFootY - 1
if var.LeftFootZ < -22 then var.LeftFootY = var.LeftFootY -1
if var.LeftFootX < -22 then var.LeftFootY = var.LeftFootY -1
if var.LeftFootX > 22 then var.LeftFootY = var.LeftFootY -1

var.LeftFootFloor = ((Var.LeftFootY < 6) and (var.LeftWasOnFloor)) or (Var.LeftFootY < 4)
var.LeftWasOnFloor = var.LeftFootFloor

// Right Foot
var.RightFootRight = var.RightFootX > 20
var.RightFootLeft = var.RightFootX < -20
var.RightFootForward = var.RightFootZ > 20
var.RightFootBack = var.RightFootZ < -20

var.RightFootUpLeft = var.RightFootForward and var.RightFootLeft
var.RightFootUpRight = var.RightFootForward and var.RightFootRight
var.RightFootDownLeft = var.RightFootBack and var.RightFootLeft
var.RightFootDownRight = var.RightFootBack and var.RightFootRight
var.RightDiagonal = var.RightFootUpLeft or var.RightFootUpRight or var.RightFootDownLeft or var.RightFootDownRight
var.RightFootCentre = (not var.RightFootRight) and (not var.RightFootLeft) and (not var.RightFootForward) and (not var.RightFootBack)

// Left Foot
var.LeftFootRight = var.LeftFootX > 20
var.LeftFootLeft = var.LeftFootX < -20
var.LeftFootForward = var.LeftFootZ > 20
var.LeftFootBack = var.LeftFootZ < -20

var.LeftFootUpLeft = var.LeftFootForward and var.LeftFootLeft
var.LeftFootUpRight = var.LeftFootForward and var.LeftFootRight
var.LeftFootDownLeft = var.LeftFootBack and var.LeftFootLeft
var.LeftFootDownRight = var.LeftFootBack and var.LeftFootRight
var.LeftDiagonal = var.LeftFootUpLeft or var.LeftFootUpRight or var.LeftFootDownLeft or var.LeftFootDownRight
var.LeftFootCentre = (not var.LeftFootRight) and (not var.LeftFootLeft) and (not var.LeftFootForward) and (not var.LeftFootBack)

// Jump when both feet are off the ground
var.Jump = (not var.RightFootFloor) and (not var.LeftFootFloor)

// Combine both feet and ensure feet are on the floor
var.Left = (var.RightFootFloor and var.RightFootLeft and not var.RightDiagonal) or (var.LeftFootFloor and var.LeftFootLeft and not var.LeftDiagonal)
var.Right = (var.RightFootFloor and var.RightFootRight and not var.RightDiagonal) or (var.LeftFootFloor and var.LeftFootRight and not var.LeftDiagonal)
var.Forward = (var.RightFootFloor and var.RightFootForward and not var.RightDiagonal) or (var.LeftFootFloor and var.LeftFootForward and not var.LeftDiagonal)
var.Back = (var.RightFootFloor and var.RightFootBack and not var.RightDiagonal) or (var.LeftFootFloor and var.LeftFootBack and not var.LeftDiagonal)
var.Centre = (var.RightFootFloor and var.RightFootCentre) or (var.LeftFootFloor and var.LeftFootCentre)
var.UpLeft = (var.RightFootFloor and var.RightFootUpLeft) or (var.LeftFootFloor and var.LeftFootUpLeft)
var.UpRight = (var.RightFootFloor and var.RightFootUpRight) or (var.LeftFootFloor and var.LeftFootUpRight)
var.DownLeft = (var.RightFootFloor and var.RightFootDownLeft) or (var.LeftFootFloor and var.LeftFootDownLeft)
var.DownRight = (var.RightFootFloor and var.RightFootDownRight) or (var.LeftFootFloor and var.LeftFootDownRight)

// Dance Dance Revolution

if not key.LeftCtrl then

key.Left = var.Left
key.Right = var.Right
key.Down = var.Back
key.Up = var.Forward
key.Enter = var.Jump
key.Home = var.UpLeft
key.PageUp = var.PageUp

// Pump It Up
if var.Pump then
  key.q = var.UpLeft
  key.e = var.UpRight
  key.z = var.DownLeft
  key.c = var.DownRight
  key.s = var.Centre
end
end if
// Debug display
debug = ""
if var.Pump then
  if var.Centre then debug = "x"
end
if var.Left then debug = "<- "
if var.Forward then debug = "^ "
if var.Back then debug = "v "
if var.Right then debug = "-> "
if var.UpLeft then debug = "'\"
if var.UpRight then debug = "/'"
if var.DownLeft then debug = ",/"
if var.DownRight then debug = "\,"

if key.LeftShift then
  debug = (var.LeftFootFloor<>False) + "   " + var.LeftFootX+', '+var.LeftFootY+', '+var.LeftFootZ
end if

if key.RightShift then
  debug = (var.RightFootFloor<>False) + "   " + var.RightFootX+', '+var.RightFootY+', '+var.RightFootZ
end if

if not key.RShift then
  //speech = Var.LeftFootFloor
end if

if key.LeftAlt then
  debug = var.LeftFootY + ",  " + Round(glove2.FilterPitch)
end if

if key.RightAlt then
  debug = var.RightFootY + ",  " + Round(glove1.FilterPitch)
end if

//debug = glove2.y +'   '+glove1.y
